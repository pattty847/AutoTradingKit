//@version=4
strategy(title="UT Bot Alerts", overlay = true)

// Inputs
a = input(1,     title = "Key Vaule. 'This changes the sensitivity'")
c = input(10,    title = "ATR Period")
h = input(false, title = "Signals from Heikin Ashi Candles")

xATR  = atr(c)
nLoss = a * xATR

src = h ? security(heikinashi(syminfo.tickerid), timeframe.period, close, lookahead = false) : close

xATRTrailingStop = 0.0
xATRTrailingStop := iff(src > nz(xATRTrailingStop[1], 0) and src[1] > nz(xATRTrailingStop[1], 0), max(nz(xATRTrailingStop[1]), src - nLoss),
   iff(src < nz(xATRTrailingStop[1], 0) and src[1] < nz(xATRTrailingStop[1], 0), min(nz(xATRTrailingStop[1]), src + nLoss), 
   iff(src > nz(xATRTrailingStop[1], 0), src - nLoss, src + nLoss)))
 
pos = 0   
pos :=	iff(src[1] < nz(xATRTrailingStop[1], 0) and src > nz(xATRTrailingStop[1], 0), 1,
   iff(src[1] > nz(xATRTrailingStop[1], 0) and src < nz(xATRTrailingStop[1], 0), -1, nz(pos[1], 0))) 
   
xcolor = pos == -1 ? color.red: pos == 1 ? color.green : color.blue 

ema   = ema(src,1)
above = crossover(ema, xATRTrailingStop)
below = crossover(xATRTrailingStop, ema)


band_type = input(title="Channel Type", defval="Bollinger Bands", options=[ "Donchian Channel", "Bollinger Bands", "Keltner Channel", "Donchian - Pivot"])
channel_length = input(20, step=5)
StdDev = input(1, step=0.5)
pvtLen = input(2)

WaitForClose  = input(true,  title="Wait For Candle Close Before Printing Pivot")
Pd  = input(22, title="ATR Periods")
mult= input(1, step=0.5, title="ATR Multiplier for Non pivot stop")
wicks = input(false)

donchian(highSource, lowSource, rangeLength)=>
    top = highest(highSource, rangeLength)
    bottom = lowest(lowSource, rangeLength)
    middle = (top+bottom)/2
    [middle, top, bottom]

f_calculatePivots(pvtLen, Shunt)=>
    pvthi_ = pivothigh(high, pvtLen, pvtLen)
    pvtlo_ = pivotlow(low, pvtLen, pvtLen)
    
    pvthi = pvthi_[Shunt]
    pvtlo = pvtlo_[Shunt]
    
    pvthighline = pvthi
    pvthighline := na(pvthighline)? nz(pvthighline[1]): pvthighline
    
    pvtlowline = pvtlo
    pvtlowline := na(pvtlowline)? nz(pvtlowline[1]): pvtlowline
    
    higherhigh = na(pvthi) ? na : ( valuewhen(pvthi, high[pvtLen+Shunt], 1) < valuewhen(pvthi, high[pvtLen+Shunt], 0) ) ? pvthi : na
    lowerhigh  = na(pvthi) ? na : ( valuewhen(pvthi, high[pvtLen+Shunt], 1) > valuewhen(pvthi, high[pvtLen+Shunt], 0) ) ? pvthi : na
    higherlow  = na(pvtlo) ? na : ( valuewhen(pvtlo, low[pvtLen+Shunt], 1)  < valuewhen(pvtlo, low[pvtLen+Shunt], 0) )  ? pvtlo : na
    lowerlow   = na(pvtlo) ? na : ( valuewhen(pvtlo, low[pvtLen+Shunt], 1)  > valuewhen(pvtlo, low[pvtLen+Shunt], 0) )  ? pvtlo : na
    
    [pvthighline, pvtlowline, higherhigh, lowerhigh, higherlow, lowerlow]

f_getBands(band_type, Periods, StdDev, pvtLen)=>
    [pvthighline, pvtlowline, higherhigh, lowerhigh, higherlow, lowerlow] = f_calculatePivots(pvtLen, 1)
    [middleBB, upperBB, lowerBB] = bb(close, Periods, StdDev)
    [middleKC, upperKC, lowerKC] = kc(close, Periods, StdDev)
    [middleDC, upperDC, lowerDC] = donchian(high, low, Periods)
    [middleDCP, upperDCP, lowerDCP] = donchian(pvthighline, pvtlowline, Periods)
    middle = band_type == "Bollinger Bands"? middleBB : band_type == "Keltner Channel"? middleKC : band_type == "Donchian Channel"? middleDC : middleDCP
    upper = band_type == "Bollinger Bands"? upperBB : band_type == "Keltner Channel"? upperKC : band_type == "Donchian Channel"? upperDC : upperDCP
    lower = band_type == "Bollinger Bands"? lowerBB : band_type == "Keltner Channel"? lowerKC : band_type == "Donchian Channel"? lowerDC : lowerDCP
    [middle, upper, lower]
    

f_get_trailing_stop(band_type, channel_length, StdDev, pvtLen, Pd, mult, wicks)=>
    [middle, upper, lower] = f_getBands(band_type, channel_length, StdDev, pvtLen)
    
    atr = atr(Pd)
    dir = 1

    longTarget = wicks? low : close
    shortTarget = wicks? high : close

    longStop = lower - atr*mult
    longStopPrev = nz(longStop[1], longStop)
    longStop := dir[1] == 1? max(longStop, longStopPrev) :longStop 

    shortStop = upper + atr*mult
    shortStopPrev = nz(shortStop[1], shortStop)
    shortStop := dir[1] == -1? min(shortStop, shortStopPrev) : shortStop
    
    dir := nz(dir[1]) == -1 and shortTarget >= shortStopPrev ? 1 : nz(dir[1]) == 1 and longTarget <= longStopPrev ? -1 : nz(dir[1], dir)
    [dir, longStop, shortStop]


[barState, buyStop, sellStop] = f_get_trailing_stop(band_type, channel_length, StdDev, pvtLen, Pd, mult, wicks)
trendColor = barState == 1? color.green : barState == -1? color.red : color.silver

trailingStop = buyStop
trailingStop := (barState == 1) ? buyStop : ((barState == -1) ? sellStop : trailingStop[1])

plot(trailingStop, title="TrailingStop", color=trendColor, linewidth=2, style=plot.style_linebr)


buy  = src > xATRTrailingStop and above and barState == 1 
sell = src < xATRTrailingStop and below and barState == -1


if buy
    strategy.entry("buy", strategy.long)

if sell 
    strategy.entry("sell", strategy.short)


plotshape(buy,  title = "Buy",  text = 'Buy',  style = shape.labelup,   location = location.belowbar, color= color.green, textcolor = color.white, transp = 0, size = size.tiny)
plotshape(sell, title = "Sell", text = 'Sell', style = shape.labeldown, location = location.abovebar, color= color.red,   textcolor = color.white, transp = 0, size = size.tiny)